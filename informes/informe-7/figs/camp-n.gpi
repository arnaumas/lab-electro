set terminal epslatex size 10cm,6cm color colortext header '\sffamily \small'
set output 'camp-n.tex'

# Format dels eixos
set xlabel '$\mathsf{N}$'
set ylabel '$\mathsf{B \ (\si{\micro T})}$'
set format '\num{%g}'
set border linewidth 1.5

# Escales
set xtics scale 0.75 1,1,3
set xrange [0:4]
set ytics scale 0.75

# Format dels punts
set style line 1 linecolor rgb "blue" pointtype 2 linewidth 1
set style line 2 linecolor rgb "blue" dashtype 2 linewidth 1

# Regressió lineal
f(x) = a*x + b
fit f(x) 'camp-n.dat' using 1:2 via a,b
stats "camp-n.dat" using 1:($2/10000000) name "A"
set label sprintf('$r^2 =$ \num{%.3f}', A_correlation) at 3.4,30 center

# Punts
plot 'camp-n.dat' using 1:2:3 notitle with errorbars linestyle 1 , [1:3] f(x) notitle with lines linestyle 2
