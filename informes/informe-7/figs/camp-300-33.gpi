set terminal epslatex size 10cm,6cm color colortext header '\sffamily \small'
set output 'camp-300-33.tex'

# Format dels eixos
set xlabel '$\mathsf{z \ (\si{cm})}$'
set ylabel '$\mathsf{B \ (\si{mT})}$'
set format '\num{%g}'
set border linewidth 1.5

# Escales
set xtics scale 0.75
set xrange [-15:15]
set yrange [0:3]
set ytics scale 0.75

mu_0 = 1.256637e-6 * 1000
I = 1
u_I = 0.01
L = 0.16
u_L = 0.01
R = 0.033/2
u_R = 0
N = 300

set style fill transparent solid 0.5 
set style line 1 linecolor rgb "light-salmon" linewidth 1
set style line 2 linecolor rgb "orange-red" dashtype 2 linewidth 1
set style line 3 linecolor rgb "gray40" pointtype -1 linewidth 1

# Camp magn√®tic
B(x) = mu_0*(I*N)/(2*L) * ((x+L)/sqrt(R**2 + (x+L)**2) - x/sqrt(R**2 + x**2))

# Error (al quadrat)
e(x) = (B(x)*u_I / I)**2 + ((mu_0*I*N/(2*L) * (R**2 / (R**2 + (x+L)**2)**(3/2)) - B(x)/L) * u_L)**2 + (mu_0*I*N/(2*L) * (x**2 / (R**2 + x**2)**(3/2) - (x+L)**2 / (R**2 + (x+L)**2)**(3/2)) * u_R)**2

plot '+' using 1:(B($1/100 - L/2) + sqrt(e($1/100 - L/2))):(B($1/100 - L/2) - sqrt(e($1/100 - L/2))) notitle with filledcurves closed linestyle 1,\
B(x/100 - L/2) notitle with lines linestyle 2,\
'camp-300-33.dat' using 1:3:2:4 notitle with xyerror linestyle 3
