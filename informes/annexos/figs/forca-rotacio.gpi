set terminal epslatex size 10cm,6cm color colortext header '\sffamily \small'
set output 'forca-rotacio.tex'

# Format dels eixos
set xlabel '$\mathsf{\theta \ (\si{\degree})}$'
set ylabel '$\mathsf{F \ (\si{mN})}$'
set format '\num{%g}'
set border linewidth 1.5

# Escales
set xtics scale 0.75
set yrange [0:0.3]
set ytics scale 0.75 0,0.05,0.3

# Format dels punts
set style line 1 linecolor rgb "blue" pointtype 2 linewidth 1
set style line 2 linecolor rgb "blue" dashtype 2 linewidth 1

# Regressió lineal
f(x) = a*x + b
fit f(x) 'forca-rotacio.dat' using 2:($1*1000) via a,b

# Punts
plot 'forca-rotacio.dat' using 2:($1*1000):3 notitle with xerrorbars linestyle 1 , f(x) notitle with lines linestyle 2
