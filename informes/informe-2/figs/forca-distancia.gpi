set terminal epslatex size 10cm,6cm color colortext header '\sffamily \small'
set output 'forca-distancia.tex'

# Format dels eixos
set xlabel '$\mathsf{1/r \ (\si{m^{-1}})}$'
set ylabel '$\mathsf{F \ (\si{mN})}$'
set format '\num{%g}'
set border linewidth 1.5

# Escales
set xtics scale 0.75
# set yrange [0:0.3]
set ytics scale 0.75

# Format dels punts
set style line 1 linecolor rgb "blue" pointtype 2 linewidth 1
set style line 2 linecolor rgb "blue" dashtype 2 linewidth 1

# Regressió lineal
f(x) = a*x + b
fit f(x) 'forca-distancia.dat' using 3:($1*1000) via a,b
set label '$r^2 = $ \num{0.932}' at 110,0.5 center 

# Punts
plot 'forca-distancia.dat' using 3:($1*1000):4:($2*1000) notitle with xyerrorbars linestyle 1 , f(x) notitle with lines linestyle 2
